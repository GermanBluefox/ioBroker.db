!# favorites.fn 1.0 CCU.IO
!#
!# Dieses Script gibt die Favoriten als JSON String aus
!#
!# 7"2013 hobbyquaker
!#


object oFavorite;
object fav;
string sFavoriteId;
string sFavoriteName;
string favId;
string chId;
boolean aFirst = true;
boolean bFirst;
Write('{');
foreach (sFavoriteId, dom.GetObject(ID_FAVORITES).EnumUsedIDs()) {
  oFavorite     = dom.GetObject(sFavoriteId);
  if (oFavorite.Name() == '_USER1004') {
    foreach(favId, oFavorite.EnumUsedIDs()) {
      if (aFirst) {
          aFirst = false;
      } else {
          WriteLine(',');
      }
      fav = dom.GetObject(favId);
      Write('"' # favId # '":{"Name":"');
      WriteURL(fav.Name());
      Write('","TypeName":"FAVORITE","FavColumnCount":' # fav.FavColumnCount());
      Write(',"FavColumnAlign":' # fav.FavColumnAlign());
      Write(',"FavNamePosition":' # fav.FavNamePosition());
      Write(',"Channels":[');
      bFirst = true;
      foreach (chId, fav.EnumUsedIDs()) {
     	if (bFirst) {
            bFirst = false;
        } else {
            Write(',');
        }
        Write(chId);
      }
      Write( ']}');
  	}
  }
}
Write('}');