!# alarms.fn 1.0 CCU.IO
!#
!# Dieses Homematic-Script gibt eine Liste aller Alarm-Datenpunkte als JSON String aus
!#
!# 10'2013 hobbyquaker https://github.com/hobbyquaker
!#

object o = dom.GetObject(ID_SERVICES);
string sDPID;

Write('{');

var dpFirst = true;

foreach (sDPID, o.EnumIDs()) {
	object datapoint = dom.GetObject(sDPID);
    var trigDP = dom.GetObject(datapoint.AlTriggerDP());
    var chDP = trigDP.Channel();

  	if (dpFirst) {
    	dpFirst = false;
    } else {
      	WriteLine(',');
    }

  	Write('"' # datapoint.ID() # '":{');
  	Write('"Name":"');
    WriteURL(datapoint.Name());
    Write('","TypeName":"ALARMDP');
    Write('","AlState":' # datapoint.AlState());
    Write(',"AlOccurrenceTime":"' # datapoint.AlOccurrenceTime());
    Write('","LastTriggerTime":"' # datapoint.LastTriggerTime());
    Write('","Operations":' # trigDP.Operations());
    Write(',"AlTriggerDP":' # trigDP.ID());
    Write(',"Parent":' # chDP);
    Write('}');
}

Write('}');